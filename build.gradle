plugins {
    id 'java'
    id 'org.springframework.boot' version '3.2.3'
    id 'io.spring.dependency-management' version '1.1.4'
}

group = 'com.benson'
version = '0.0.1-SNAPSHOT'

configurations {
    jaxb
}

bootJar {
    archiveBaseName = 'gs-producing-web-service'
    archiveVersion = '0.1.0'
}

java {
    sourceCompatibility = '17'
    sourceSets.main.java.srcDirs += "$buildDir/generated-sources/jaxb"
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-web-services'
    implementation 'wsdl4j:wsdl4j'
    compileOnly 'org.projectlombok:lombok'
    jaxb("org.glassfish.jaxb:jaxb-xjc")
    developmentOnly 'org.springframework.boot:spring-boot-devtools'
    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
    annotationProcessor 'org.projectlombok:lombok'
    runtimeOnly 'org.springframework.boot:spring-boot-starter-tomcat'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

tasks.register('genJaxb') {
    // Define a list of xsd files and their output package
    ext.schemas = [["src/main/resources/countries.xsd", "com.jaxb.countries"],
                   ["src/main/resources/people.xsd", "com.jaxb.people"]]

    ext.sourcesDir = "${buildDir}/generated-sources/jaxb"

    doFirst {
        delete(sourcesDir)
    }

    doLast {
        project.ant {
            taskdef name: "xjc",
                    classname: "com.sun.tools.xjc.XJCTask",
                    classpath: configurations.jaxb.asPath
            mkdir(dir: sourcesDir)

            schemas.each { schema ->
                xjc(destdir: sourcesDir, schema: schema[0], package: schema[1]) {
                    arg(value: "-XautoNameResolution")
                }
            }
        }
    }
}

compileJava.dependsOn(genJaxb)
tasks.named('test') {
    useJUnitPlatform()
}